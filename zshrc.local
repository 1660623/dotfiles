#!/bin/zsh

### PATH

# Properly order the default path
export PATH=/usr/local/bin:/usr/local/sbin:$PATH

### Color

# Use colorized ls
if [[ $(uname -s) == 'Darwin' ]]; then
    export CLICOLOR=1
    export LSCOLORS='Exfxcxdxbxegedabagacad'
else
    alias ls='ls --color=auto'
fi

### rcm

# Ensure the HOSTNAME environment variable is set so that `rcm` works properly
if [[ -z "$HOSTNAME" ]]; then
    export HOSTNAME=$(hostname -s)
fi

### ZSH Autocorrect

# Don't autocorrect arguments
unsetopt correct_all

### ZSH Completion

# Use case-insensitive completion matches
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

### PROMPT

# Adds an indicator of where the HEAD is in the graph.
#
# If HEAD is on a branch, the branch name is output in green. If HEAD is
# on a tag, the tag name is output in yellow. If HEAD is on neither a
# branch nor a tag, the short SHA is output in yellow.
git_prompt_info() {
    ref=$(git symbolic-ref HEAD 2> /dev/null)
    if [[ -n $ref ]]; then
        echo "[%{$fg_bold[green]%}${ref#refs/heads/}%{$reset_color%}]"
    else
        tag=$(git branch --no-color 2> /dev/null |
                  sed -e '/^[^*]/d' -e "s/* (detached from \(.*\))/\1/")

        if [[ -n $tag ]]; then
            echo "[%{$fg_bold[yellow]%}${tag}%{$reset_color%}]"
        fi
    fi
}

exit_code() {
    echo "%(?..[${fg_bold[red]}%?%{$reset_color%}])"
}

export PS1='$(git_prompt_info)[${SSH_CONNECTION+"%{$fg_bold[green]%}%n@%m:"}%{$fg_bold[blue]%}%~%{$reset_color%}]$(exit_code) '

### Other Initializations

# Perform any app-specific initializations
for init in $HOME/.zsh/init/*.sh; do
    source "$init"
done
