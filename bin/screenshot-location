#!/usr/bin/env elixir

# Changes the default location for storing screenshots using the OS X keyboard commands.
#
# From: http://osxdaily.com/2011/01/26/change-the-screenshot-save-file-location-in-mac-os-x/

defmodule Defaults do
  def read(args) do
    results = System.cmd("defaults", ["read"] ++ OptionParser.split(args), stderr_to_stdout: true)
    read_results(results, args)
  end

  def write(args) do
    {output, 0} = System.cmd("defaults", ["write"] ++ OptionParser.split(args))
    output
  end

  defp read_results({output, 0}, _), do: {:ok, output}
  defp read_results({_, 1}, args), do: {:error, "Default for '#{args}' does not exist\n"}
end

defmodule ScreenshotLocation do
  def execute([default: true], []), do: write_location("~/Desktop")
  def execute([default: true], _), do: display_error(:default_and_location)
  def execute(_, [location]), do: write_location(location)
  def execute(_, []), do: read_location()
  def execute(_, args) when is_list(args), do: display_error(:more_than_one_location)

  defp display_error(:default_and_location), do: IO.puts("Cannot specify 'default' and a location together")
  defp display_error(:more_than_one_location), do: IO.puts("Cannot specify more than one location")

  defp display_location({:ok, output}), do: IO.puts(String.trim(output))
  defp display_location({:error, _}), do: IO.puts(Path.join(System.get_env("HOME"), "Desktop"))

  defp read_location do
    "com.apple.screencapture location"
    |> Defaults.read
    |> display_location()
  end

  defp write_location(location) do
    location = Path.expand(location)

    "com.apple.screencapture location #{location}/"
    |> Defaults.write
    |> IO.puts
  end
end

{options, args, _} = OptionParser.parse(System.argv(), strict: [default: :boolean])
ScreenshotLocation.execute(options, args)
